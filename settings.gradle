rootProject.name = 'osh-addons'
gradle.ext.oshCoreDir = "$rootDir/../osh-core"

if (!new File(gradle.oshCoreDir).exists())
    throw new GradleException('Repository osh-core is missing. Please clone it from https://github.com/opensensorhub/osh-core')

// include osh-core build only if not part of larger build
// this is needed to avoid nested composite builds
// comment this to build against published artifacts
if (gradle.parent == null)
  includeBuild gradle.oshCoreDir

// include all folders with build.gradle as subprojects
def excludedProjects = [
  'sensorhub-storage-ndbc'
]
FileTree subprojects = fileTree(rootDir).exclude('dist').include('**/build.gradle')
subprojects.files.each { File f ->
  File projectFolder = f.parentFile
  if (projectFolder != rootDir && !f.absolutePath.contains("ros")) {
    String projectName = ':' + projectFolder.name
    //String projectName = ':' + rootDir.toPath().relativize(projectFolder.toPath()).toString().replace('/', ':')
    //println projectName
    if (!excludedProjects.contains(projectName.substring(1))) {
      include projectName
      project(projectName).projectDir = projectFolder
    }
  }
}
